# Домашнее задание к занятию «Организация сети»


### Задание 1. Yandex Cloud 

**Что нужно сделать**

1. Создать пустую VPC. Выбрать зону.
2. Публичная подсеть.

 - Создать в VPC subnet с названием public, сетью 192.168.10.0/24.
 - Создать в этой подсети NAT-инстанс, присвоив ему адрес 192.168.10.254. В качестве image_id использовать fd80mrhj8fl2oe87o4e1.
 - Создать в этой публичной подсети виртуалку с публичным IP, подключиться к ней и убедиться, что есть доступ к интернету.
3. Приватная подсеть.
 - Создать в VPC subnet с названием private, сетью 192.168.20.0/24.
 - Создать route table. Добавить статический маршрут, направляющий весь исходящий трафик private сети в NAT-инстанс.
 - Создать в этой приватной подсети виртуалку с внутренним IP, подключиться к ней через виртуалку, созданную ранее, и убедиться, что есть доступ к интернету.

Resource Terraform для Yandex Cloud:

- [VPC subnet](https://registry.terraform.io/providers/yandex-cloud/yandex/latest/docs/resources/vpc_subnet).
- [Route table](https://registry.terraform.io/providers/yandex-cloud/yandex/latest/docs/resources/vpc_route_table).
- [Compute Instance](https://registry.terraform.io/providers/yandex-cloud/yandex/latest/docs/resources/compute_instance).

### Ответ  

[variables.tf](yc/variables.tf)  
[main.tf](yc/main.tf)  
[cloud_init_config.tpl](yc/cloud_init_config.tpl)  

Описание:   
в variables.tf  задаются переменные (id облака, папки, образа а так же имя пользователя и пути сохранения ssh ключей)  

в main.tf 

1. Определяем параметры провайдера 
```
terraform {
  required_providers {
    yandex = {
      source  = "yandex-cloud/yandex"
    }
  }
}
provider "yandex" {
  service_account_key_file = "key.json"
  cloud_id                 = var.yandex_cloud_id
  folder_id                = var.yandex_folder_id
  zone                     = var.yandex_zone
}
```

2. Генерируем ключи ssh и сохраняем на локальную машину приватный ключ  
```
resource "tls_private_key" "ssh-key" {
  algorithm = "RSA"
  rsa_bits  = 4096
}
resource "local_sensitive_file" "id_rsa" {
  filename = "ssh_key"
  file_permission = "600"
  content = tls_private_key.ssh-key.private_key_pem
}
```
3. Генерируем cloud-init конфиг с помощью шаблона cloud_init_config.tpl для ВМ с указанием имени пользователя и добавлением ssh ключа для подключения. В дальнейшем будем использовать его при создании ВМ в блоке metadata
```
data "template_file" "cloud_init" {
  template = file("cloud_init_config.tpl")
  vars = {
    user    = var.user
    ssh_key = tls_private_key.ssh-key.public_key_openssh
  }
}
```
4. Создаём пустую VPC  и публичную подсеть public
```
esource "yandex_vpc_network" "yc-vpc" {
  name        = "yc_vpc_network"    
}

resource "yandex_vpc_subnet" "yc-subnet-a" {
  name           = "public"
  description    = "public-net"
  v4_cidr_blocks = ["192.168.10.0/24"]
  zone                     = var.yandex_zone
  network_id     = "${yandex_vpc_network.yc-vpc.id}"
}
```
5. Создаём NAT - инстанс с локальным ип 192.16.10.254 и публичным IP (nat = true). Образ fd8oi1ha2tq6bh8tfco4. В блоке metadata указан сгенерированный конфиг.  

```
resource "yandex_compute_instance" "nat-instance" {
  name = "nat-instance"
  zone = var.yandex_zone
  resources {
    cores  = 2
    memory = 2
  }
  boot_disk {
    initialize_params {
      image_id = "fd8oi1ha2tq6bh8tfco4"
      size     = 20
    }
  }
  network_interface {
    subnet_id = "${yandex_vpc_subnet.yc-subnet-a.id}"
    nat       = true
    ip_address = "192.168.10.254"
  }
  metadata = {
    user-data = data.template_file.cloud_init.rendered
  }
}
```
6. Создаём виртуалку c публичным IP. Через  provisioner "file" копируем приватный ключ SSH и назначаем на него права через provisioner "remote-exec". Это необходимо что бы потом можно было подключиться на виртуалку с только внутренним IP

```
resource "yandex_compute_instance" "test-vm-pub" {
  name = "test-vm-pub"
  zone = var.yandex_zone
  resources {
    cores  = 2
    memory = 2
  }
  boot_disk {
    initialize_params {
      image_id = var.iso_id
      size     = 20
    }
  }
  network_interface {
    subnet_id = "${yandex_vpc_subnet.yc-subnet-a.id}"
    nat       = true
  }
  metadata = {
    user-data = data.template_file.cloud_init.rendered
  }
  provisioner "file" {
    content = tls_private_key.ssh-key.private_key_pem
    destination = pathexpand(var.private_key_path)
    connection {
      type        = "ssh"
      host        = self.network_interface.0.nat_ip_address
      user        = var.user
      private_key = tls_private_key.ssh-key.private_key_openssh
    }
  }
  provisioner "remote-exec" {
    inline = [
      "chmod 600 /home/rkhozyainov/.ssh/id_rsa",
    ]
    connection {
      type        = "ssh"
      host        = self.network_interface.0.nat_ip_address
      user        = var.user
      private_key = tls_private_key.ssh-key.private_key_openssh
    }
  }
}  
```
7. Создаём вторую подсеть private, таблицу маршрутизации и привязываем подсеть к таблице маршрутизации.
```
resource "yandex_vpc_subnet" "yc-subnet-b" {
  name           = "private"
  description    = "private-net"
  v4_cidr_blocks = ["192.168.20.0/24"]
  zone                     = var.yandex_zone
  network_id     = "${yandex_vpc_network.yc-vpc.id}"
  route_table_id = yandex_vpc_route_table.yc-rt.id
}

resource "yandex_vpc_route_table" "yc-rt" {
  name       = "yc-rt"
  network_id = "${yandex_vpc_network.yc-vpc.id}"

  static_route {
    destination_prefix = "0.0.0.0/0"
    next_hop_address   = "192.168.10.254"
  }
}
```
8. Создаём виртуалку в приватной сети без публичного ип 
```
resource "yandex_compute_instance" "test-vm-priv" {
  name = "test-vm-priv"
  zone = var.yandex_zone
  resources {
    cores  = 2
    memory = 2
  }
  boot_disk {
    initialize_params {
      image_id = var.iso_id
      size     = 20
    }
  }
  network_interface {
    subnet_id = "${yandex_vpc_subnet.yc-subnet-b.id}"
  }
  metadata = {
    user-data = data.template_file.cloud_init.rendered
  }
}
```

Проверка:

```bash
rkhozyainov@rkhozyainov-T530-ubuntu:~/devops/clopro-homeworks/15.1/yc$ terraform apply -auto-approve
...
...
Plan: 9 to add, 0 to change, 0 to destroy.
...
...
yandex_compute_instance.test-vm-pub: Creation complete after 59s [id=fhms8h65d4gphj050h6d]

Apply complete! Resources: 9 added, 0 changed, 0 destroyed.
rkhozyainov@rkhozyainov-T530-ubuntu:~/devops/clopro-homeworks/15.1/yc$ ssh -i ./ssh_key 51.250.88.46
rkhozyainov@fhms8h65d4gphj050h6d:~$ ping ya.ru
PING ya.ru (5.255.255.242) 56(84) bytes of data.
64 bytes from ya.ru (5.255.255.242): icmp_seq=1 ttl=58 time=0.711 ms
64 bytes from ya.ru (5.255.255.242): icmp_seq=2 ttl=58 time=0.417 ms
^C
--- ya.ru ping statistics ---
2 packets transmitted, 2 received, 0% packet loss, time 1002ms
rtt min/avg/max/mdev = 0.417/0.564/0.711/0.147 ms
rkhozyainov@fhms8h65d4gphj050h6d:~$ ssh 192.168.20.17
rkhozyainov@fhmsa1a35s5hca4irul3:~$ ping ya.ru 
PING ya.ru (77.88.55.242) 56(84) bytes of data.
64 bytes from ya.ru (77.88.55.242): icmp_seq=1 ttl=249 time=4.97 ms
64 bytes from ya.ru (77.88.55.242): icmp_seq=2 ttl=249 time=4.01 ms
64 bytes from ya.ru (77.88.55.242): icmp_seq=3 ttl=249 time=4.00 ms
64 bytes from ya.ru (77.88.55.242): icmp_seq=4 ttl=249 time=4.10 ms
^C
--- ya.ru ping statistics ---
4 packets transmitted, 4 received, 0% packet loss, time 3004ms
rtt min/avg/max/mdev = 3.999/4.267/4.966/0.404 ms
```

